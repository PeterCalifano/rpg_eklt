# Common helpers for cross toolchains. Callers must set TOOLCHAIN_TRIPLE.

if(NOT DEFINED TOOLCHAIN_TRIPLE)
  message(FATAL_ERROR "TOOLCHAIN_TRIPLE must be set before including common-cross.cmake")
endif()

if(NOT DEFINED TOOLCHAIN_SYSTEM_NAME)
  set(TOOLCHAIN_SYSTEM_NAME Linux)
endif()

if(NOT DEFINED TOOLCHAIN_SYSTEM_PROCESSOR)
  string(REGEX REPLACE "-.*" "" _arch "${TOOLCHAIN_TRIPLE}")
  set(TOOLCHAIN_SYSTEM_PROCESSOR "${_arch}")
endif()
set(TOOLCHAIN_SYSTEM_PROCESSOR "${TOOLCHAIN_SYSTEM_PROCESSOR}" CACHE STRING "Target processor")


set(CMAKE_SYSTEM_NAME "${TOOLCHAIN_SYSTEM_NAME}")
set(CMAKE_SYSTEM_PROCESSOR "${TOOLCHAIN_SYSTEM_PROCESSOR}")

if(NOT DEFINED TOOLCHAIN_ROOT AND DEFINED ENV{TOOLCHAIN_ROOT})
  set(TOOLCHAIN_ROOT "$ENV{TOOLCHAIN_ROOT}")
endif()
set(TOOLCHAIN_ROOT "${TOOLCHAIN_ROOT}" CACHE PATH "Path to cross toolchain root (optional)")

if(NOT DEFINED CMAKE_SYSROOT AND DEFINED ENV{SYSROOT})
  set(CMAKE_SYSROOT "$ENV{SYSROOT}")
endif()
set(CMAKE_SYSROOT "${CMAKE_SYSROOT}" CACHE PATH "Path to sysroot (optional)")

if(NOT DEFINED CMAKE_TRY_COMPILE_TARGET_TYPE)
  set(CMAKE_TRY_COMPILE_TARGET_TYPE STATIC_LIBRARY)
endif()

set(_toolchain_paths)
list(APPEND _toolchain_paths
  "/usr/bin/${TOOLCHAIN_TRIPLE}"
  "/usr/${TOOLCHAIN_TRIPLE}/bin"
  "/usr/local/${TOOLCHAIN_TRIPLE}/bin"
  "/opt/${TOOLCHAIN_TRIPLE}/bin"
  "/usr/bin"
  "/usr/local/bin"
  "/opt/bin"
)
if(TOOLCHAIN_PATH_HINTS)
  list(APPEND _toolchain_paths ${TOOLCHAIN_PATH_HINTS})
endif()
if(TOOLCHAIN_ROOT)
  list(APPEND _toolchain_paths "${TOOLCHAIN_ROOT}/bin")
endif()

if(CMAKE_SYSROOT)
  list(PREPEND CMAKE_FIND_ROOT_PATH "${CMAKE_SYSROOT}")
endif()
if(TOOLCHAIN_ROOT)
  list(PREPEND CMAKE_FIND_ROOT_PATH "${TOOLCHAIN_ROOT}")
endif()

set(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM NEVER)
set(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY)
set(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)
set(CMAKE_FIND_ROOT_PATH_MODE_PACKAGE ONLY)
